;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; NSIS installer script for vlc ;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;
; CHANGELOG ;
;;;;;;;;;;;;;
; modifications by Gildas Bazin <gbazin@netcourrier.com> 6 Jul 2003
; . Start menu shortcuts now created in the "All Users" section.
; modifications by Gildas Bazin <gbazin@netcourrier.com> 6 Apr 2003
; . added shortcut to the skins interface, added the default skin.
; modifications by Gildas Bazin <gbazin@netcourrier.com> 26 Feb 2003
; . added the locale directory.
; modifications by Samuel Hocevar <sam@zoy.org> 05 Apr 2002
; . no need for @VERSION@ anymore
; . updated script for NSIS 1.96.
; modifications by Samuel Hocevar <sam@zoy.org> 12 Nov 2001
; . removed version information.
; written by Pierre Baillet <oct@zoy.org> 29 Jul 2001 v0.2
; . lists all installed files so as to prevent installer.nsi to be included
;   in the release.
; . really remove the shortcuts.
; . added an exec command to display the program file folder. (grOovy)

;;;;;;;;;;;;;;;;;;;;;;;;;
; General configuration ;
;;;;;;;;;;;;;;;;;;;;;;;;;

OutFile ..\vlc-${VERSION}-win32.exe
SetCompressor bzip2

Name "VLC media player"
Caption "VLC '${VERSION}' for Win32 Setup"
;Icon ../share/gvlc32x32.ico

CRCCheck on

InstallDir "$PROGRAMFILES\VideoLAN\VLC"

; check if the program has already been installed, if so, take this dir
; as install dir
InstallDirRegKey HKLM SOFTWARE\VideoLAN\VLC "Install_Dir"
DirText "Choose a directory to install in to:"

AutoCloseWindow false
ShowInstDetails show

SetOverwrite ifnewer
SetDatablockOptimize on
SetDateSave off
SetCompress auto

UninstallText "This will uninstall the VLC media player."

;;;;;;;;;;;;;;;;;;;;
; Install settings ;
;;;;;;;;;;;;;;;;;;;;

Section "Install"
	SetOutPath $INSTDIR

	File  vlc.exe
	File  *.txt

	File  /r share
	File  /r plugins
	File  /r locale
	File  /r skins

        SetShellVarContext all

	CreateDirectory "$SMPROGRAMS\VideoLAN"
	CreateShortCut "$SMPROGRAMS\VideoLAN\vlc.lnk" \
                       "$INSTDIR\vlc.exe" "--intf wxwin"
	CreateShortCut "$SMPROGRAMS\VideoLAN\vlc (skins).lnk" \
                       "$INSTDIR\vlc.exe" "--intf skins"
	CreateShortCut "$SMPROGRAMS\VideoLAN\vlc (custom).lnk" \
                       "$INSTDIR\vlc.exe" 

        WriteRegStr HKLM \
            SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\VideoLAN \
            "DisplayName" "VideoLAN VLC media player"
        WriteRegStr HKLM \
            SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\VideoLAN \
            "UninstallString" '"$INSTDIR\uninstall.exe"'
	WriteUninstaller "uninstall.exe"

	WriteRegStr HKLM SOFTWARE\VideoLAN\VLC "Install_Dir" $INSTDIR
	WriteRegStr HKLM SOFTWARE\VideoLAN\VLC "version" "${VERSION}"

	Exec "explorer $SMPROGRAMS\VideoLAN\"
SectionEnd

;;;;;;;;;;;;;;;;;;;;;;
; Uninstall settings ;
;;;;;;;;;;;;;;;;;;;;;;

Section "Uninstall"
        SetShellVarContext all
	RMDir /r $SMPROGRAMS\VideoLAN
	RMDir /r $INSTDIR
	DeleteRegKey HKLM SOFTWARE\VideoLAN
        DeleteRegKey HKLM \
            Software\Microsoft\Windows\CurrentVersion\Uninstall\VideoLAN
SectionEnd
